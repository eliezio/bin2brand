buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                [group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: springBootVersion],
                [group: 'ca.cutterslade.gradle', name: 'gradle-dependency-analyze', version: '1.0.4'],
        )
    }
}

plugins {
    // Basic Plugins
    id 'java'
    id 'groovy'
    id 'application'
    id 'project-report'
    id 'jacoco'

    // Dependency Management Support Plugins
    id 'com.github.ben-manes.versions' version '0.13.0'

    // Versioning Plugins
    id 'nebula.release' version '4.1.0'
    id 'nebula.info' version '3.3.3'
    id 'com.gorylenko.gradle-git-properties' version '1.4.17'

    // Quality / Documentation Plugins
    id 'org.sonarqube' version '2.1'
    id 'org.asciidoctor.convert' version '1.5.3'

    // Docker
    id 'com.bmuschko.docker-java-application' version '3.0.3'
}

apply plugin: 'spring-boot'
apply plugin: 'ca.cutterslade.analyze'
apply plugin: 'com.bmuschko.docker-java-application'

apply from: 'gradle/integration-test.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/sonarqube.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/dependency-analyze.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'io.eliez.fintools'
mainClassName = "${project.group}.${project.name}.boot.Application"

ext {
    // Required for Spock snapshot verions
    sonatypeSnapshotsRepo = 'https://oss.sonatype.org/content/repositories/snapshots'
    spockReportsVersion = '1.2.13'
    spockVersion = '1.1-groovy-2.4-SNAPSHOT'
    validationApiVersion = '1.1.0.Final'

    dockerOrganization = 'ebo'
}

repositories {
    jcenter()
    maven { url sonatypeSnapshotsRepo }
}

configurations {
    compile {
        exclude module: 'spring-boot-starter-tomcat'
        exclude module: 'groovy'
    }
}

dependencies {
    compileOnly(
            [group: 'org.projectlombok', name: 'lombok'],
    )
    compile(
            [group: 'one.util', name: 'streamex', version: '0.6.2'],

            [group: 'org.springframework', name: 'spring-context'],
            [group: 'org.springframework', name: 'spring-web'],
            [group: 'org.springframework.boot', name: 'spring-boot'],
            [group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'],
            [group: 'javax.validation', name: 'validation-api', version: validationApiVersion],
            [group: 'javax.servlet', name: 'javax.servlet-api'],
    )
    runtime(
            [group: 'org.springframework.boot', name: 'spring-boot-starter-web'],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-validation'],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'],
            [group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'],

            [group: 'com.fasterxml.jackson.core', name: 'jackson-core'],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-databind'],

            [group: 'ch.qos.logback', name: 'logback-classic'],
            [group: 'ch.qos.logback', name: 'logback-access'],
    )
    testCompileOnly(
            [group: 'org.projectlombok', name: 'lombok'],
    )
    testCompile(
            [group: 'org.codehaus.groovy', name: 'groovy-all'],
            [group: 'org.spockframework', name: 'spock-core', version: spockVersion],
    )
    testRuntime(
            [group: 'com.athaydes', name: 'spock-reports', version: spockReportsVersion],
    )

    integrationTestCompileOnly(
            [group: 'org.projectlombok', name: 'lombok'],
    )
    integrationTestCompile(
            [group: 'org.codehaus.groovy', name: 'groovy-all'],
            [group: 'org.spockframework', name: 'spock-core', version: spockVersion],
            [group: 'org.springframework', name: 'spring-beans'],
            [group: 'org.springframework', name: 'spring-test'],
            [group: 'org.springframework.boot', name: 'spring-boot-test-autoconfigure'],
            [group: 'org.hamcrest', name: 'hamcrest-core'],
            [group: 'org.springframework.restdocs', name: 'spring-restdocs-core'],
            [group: 'org.springframework.restdocs', name: 'spring-restdocs-mockmvc'],

            // Required by org.springframework.test.web.servlet.result.MockMvcResultHandlers that references
            // org.apache.commons.logging.Log
            // Only happens with the Groovy compiler!
            [group: 'org.slf4j', name: 'jcl-over-slf4j'],
    )
    integrationTestRuntime(
            [group: 'org.spockframework', name: 'spock-spring', version: spockVersion],
            [group: 'org.springframework.boot', name: 'spring-boot-test'],
            [group: 'org.mockito', name: 'mockito-core'],
            [group: 'com.jayway.jsonpath', name: 'json-path'],
            [group: 'com.athaydes', name: 'spock-reports', version: spockReportsVersion],
    )
}

analyzeClassesDependencies {
    justWarn = true
}

analyzeTestClassesDependencies {
    justWarn = true
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

springBoot {
    buildInfo()
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }
}

distributions {
    main {
        contents {
            from('config') {
                into 'config'
            }
        }
    }
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

dockerDistTar {
    environmentVariable 'JAVA_OPTS', '-Xmx384m -Djava.security.egd=file:/dev/./urandom'
}

docker {
    url = System.properties['docker.host']
    javaApplication {
        baseImage = 'davidcaste/alpine-java-unlimited-jce'
        maintainer = 'Eliezio Oliveira "ebo@pobox.com"'
        tag = System.properties['docker.registry'] + "${dockerOrganization}/${project.name}:" + ("${version}" - ~/-.*/)
    }
}

